.include "../nominal.jsim"
.include "../stdcell.jsim"

.subckt mult4 a[3:0] b[3:0] p[3:0]

* Partial products
Xpp0 b[0]#4 a[3:0] pp0[3:0] and2
Xpp1 b[1]#3 a[2:0] pp1[2:0] and2
Xpp2 b[2]#2 a[1:0] pp2[1:0] and2
Xpp3 b[3]   a[0]   pp3[0]   and2

* Sum of partial products
.connect pp0[0] p[0]

Xfa10 pp0[1] pp1[0] 0 p[1] co1[0] full_adder

Xfa20 pp0[2] pp1[1] co1[0] sum2[0] co2[0] full_adder
Xfa21 sum2[0] pp2[0] 0 p[2] co2[1] full_adder

Xfa30 pp0[3] pp1[2] co2[0] sum3[0] co3[0] full_adder
Xfa31 sum3[0] pp2[1] co2[1] sum3[1] co3[1] full_adder
Xfa32 sum3[1] pp3[0] 0 p[3] co3[2] full_adder

.ends

* Full adder
.subckt full_adder a b ci s co

* sum
Xs1 a b s1 xor2
Xs2 s1 ci s xor2

* carry out
Xc1 a b c1 and2
Xc2 a ci c2 and2
Xc3 b ci c3 and2
Xc4 c1 c2 c4 or2
Xc5 c3 c4 co or2

.ends

* Test

Wa a3 a2 a1 a0 nrz(0,3.3,160n,0n,.1n,.1n)
+ 0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15

Wb b3 b2 b1 b0 nrz(0,3.3,160n,0n,.1n,.1n)
+ 0 1 2 3 4 5 6 7 8 9 10 11 12 13 14 15

Xtest_mult4 a[3:0] b[3:0] prod[3:0] mult4
.tran 20ns
.plot a3
.plot a2
.plot a1
.plot a0
.plot b3
.plot b2
.plot b1
.plot b0
.plot prod3
.plot prod2
.plot prod1
.plot prod0

